#!/bin/zsh

host=$1
shift 2> /dev/null || {
    echo "Usage: $0 HOST"
    exit 1
}

count=0
total=0
while read url code location; do
    [[ -n $url ]] || continue
    source=${${${url#http://}#https://}%%/*}
    for version in ipv4 ipv6; do
        echo -n "Testing $url ($version)... "
        read seconds gcode glocation <<(curl --$version \
                                             -w '%{time_total} %{http_code} %{redirect_url}\n' \
                                             -s -o /dev/null \
                                             --fail \
                                             --max-time 2 \
                                             --connect-to ${source}:443:$host:443 \
                                             --connect-to ${source}:80:$host:80 \
                                             $url)
        if [[ "$gcode $glocation" != "$code $location" ]]; then
            echo "â˜ ï¸: $(wdiff -n =(echo $gcode${glocation:+ }$glocation) =(echo $code${location:+ }$location) | colordiff)"
        else
            ((seconds > 1000)) && ((seconds/=1000000.))
            ((count+=1,total+=seconds))
            # ~ = not, ~~ = truncate
            echo "ðŸ‘Œ ($((~~(seconds*1000)))ms)"
        fi
    done
done <<EOF
https://bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
https://bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
https://vincent.bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://bernat.ch/keepalive 301 https://bernat.ch/keepalive
http://bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://vincent.bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://vincent.bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
https://vincent.bernat.ch/keepalive 200
https://vincent.bernat.ch/ 301 https://vincent.bernat.ch/en
https://vincent.bernat.ch/$ 404
http://www.luffy.cx/keepalive 301 https://www.luffy.cx/keepalive
https://www.luffy.cx/keepalive 301 https://vincent.bernat.ch/keepalive
http://luffy.cx/keepalive 301 https://www.luffy.cx/keepalive
https://luffy.cx/keepalive 301 https://www.luffy.cx/keepalive
http://media.luffy.cx/images/favicon.png 301 https://media.luffy.cx/images/favicon.png
https://media.luffy.cx/images/favicon.png 200
https://media.bernat.ch/images/favicon.png 200
https://bernat.ch/.well-known/webfinger 404
https://bernat.ch/.well-known/webfinger?resource=acct:vincent@bernat.ch 302 https://hachyderm.io/.well-known/webfinger?resource=acct:vbernat@hachyderm.io

https://comments.luffy.cx/?uri=%2Ffr%2Fblog%2F2018-routage-l3-hyperviseur&nested_limit=5 200
https://comments.luffy.cx/?uri=%2Ffr%2Fblog%2F2018-nothing&nested_limit=5 200

http://une-oasis-une-ecole.fr/contact 301 https://www.une-oasis-une-ecole.fr/contact
https://une-oasis-une-ecole.fr/contact 301 https://www.une-oasis-une-ecole.fr/contact
https://www.une-oasis-une-ecole.fr/contact 200
http://media.une-oasis-une-ecole.fr/images/l/favicon.png 301 https://media.une-oasis-une-ecole.fr/images/l/favicon.png
https://media.une-oasis-une-ecole.fr/images/l/favicon.png 200

http://haproxy.debian.net/haproxy.css 301 https://haproxy.debian.net/haproxy.css
https://haproxy.debian.net/haproxy.css 200
http://haproxy.debian.net/dists/stretch-backports-1.9/Release 200
https://haproxy.debian.net/dists/stretch-backports-1.9/Release 200

http://insolites-en-mene.pages.luffy.cx 301 https://insolites-en-mene.pages.luffy.cx/
https://insolites-en-mene.pages.luffy.cx 200
https://pages.luffy.cx 403
EOF
[ $count -gt 0 ] && \
    echo "Average response time: $((~~(total*1000/count)))ms"
