#!/bin/zsh

host=$1
shift 2> /dev/null || {
    echo "Usage: $0 HOST"
    exit 1
}

count=0
total=0
while read url code location; do
    [[ -n $url ]] || continue
    source=${${${url#http://}#https://}%%/*}
    echo -n "Testing $url... "
    read seconds gcode glocation <<(curl -s -w '%{time_total} %{http_code} %{redirect_url}\n' -o /dev/null \
                                --fail \
                                --max-time 2 \
                                --connect-to ${source}:443:$host:443 \
                                --connect-to ${source}:80:$host:80 \
                                $url)
    if [[ "$gcode $glocation" != "$code $location" ]]; then
        echo "☠️: $(wdiff -n =(echo $gcode${glocation:+ }$glocation) =(echo $code${location:+ }$location) | colordiff)"
    else
        ((count+=1,total+=seconds))
        echo "👌 ($((~~(seconds*1000)))ms)"
    fi
done <<EOF
https://bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
https://bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
https://www.bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
https://www.bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
https://vincent.bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
http://bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://www.bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
http://www.bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://vincent.bernat.im/keepalive 301 https://vincent.bernat.ch/keepalive
http://vincent.bernat.ch/keepalive 301 https://vincent.bernat.ch/keepalive
https://vincent.bernat.ch/keepalive 200
https://vincent.bernat.ch/$ 404
http://www.luffy.cx/keepalive 301 https://www.luffy.cx/keepalive
https://www.luffy.cx/keepalive 301 https://vincent.bernat.ch/keepalive
http://luffy.cx/keepalive 301 https://www.luffy.cx/keepalive
https://luffy.cx/keepalive 301 https://www.luffy.cx/keepalive
http://media.luffy.cx/css/common.css 301 https://media.luffy.cx/css/common.css
https://media.luffy.cx/css/common.css 200

http://une-oasis-une-ecole.fr/contact 301 https://www.une-oasis-une-ecole.fr/contact
https://une-oasis-une-ecole.fr/contact 301 https://www.une-oasis-une-ecole.fr/contact
https://www.une-oasis-une-ecole.fr/contact 200
http://media.une-oasis-une-ecole.fr/css/common.css 301 https://media.une-oasis-une-ecole.fr/css/common.css
https://media.une-oasis-une-ecole.fr/css/common.css 200

http://haproxy.debian.net/haproxy.css 301 https://haproxy.debian.net/haproxy.css
https://haproxy.debian.net/haproxy.css 200
http://haproxy.debian.net/dists/stretch-backports-1.9/Release 200
https://haproxy.debian.net/dists/stretch-backports-1.9/Release 200
EOF
[ $count -gt 0 ] && \
    echo "Average response time: $((~~(total*1000/count)))ms"
